buildscript {
    ext.kotlin_version = '1.0.3'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

group 'io.dev.temperature'
version '2.0.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/cy6ergn0m/maven" }
}

dependencies {
    compile 'com.h2database:h2:1.4.191'

    compile 'io.vertx:vertx-core:3.2.1'
    compile 'io.vertx:vertx-web:3.2.1'
    compile 'io.vertx:vertx-jdbc-client:3.2.1'

    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.pi4j:pi4j-core:1.0"

    testCompile 'junit:junit:4.11'
    testCompile 'io.vertx:vertx-unit:3.2.1'

}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

jar {
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'io.dev.temperature.TemperatureApp'
    }
}

shadowJar {
    classifier = 'fat'

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task prepareDist(description: "Prepares zip with distributions file", type: Zip, dependsOn: [clean, shadowJar]) {
    archiveName = "temperature-${version}-dist.zip"
    destinationDir = new File(buildDir, "dist")

    from("build/libs") {
        include '*-fat.jar'
    }
    from("config/") {
        include '*'
    }
    from("scripts/") {
        include '**/*'
    }
}

